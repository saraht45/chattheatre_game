<object clone="/usr/SkotOS/obj/meracha" owner="SkotOS">
  <SkotOS:MerAcHa action_id="wield" verb="OBJ(Allegory:Neoct:Verbs:W:wield)">
    <Core:PCProperties>
      <Core:Property property="merry:global-desc:action">
         X[M] return FALSE;
      </Core:Property>
      <Core:Property property="merry:global:action">
         X[M] object weapon, mainhand, offhand;
int size;

weapon = NRefOb(\$target);

if(!weapon."base:weapon") \{
    EmitTo(\$actor, "You cannot wield " + Describe(weapon, nil, nil, STYLE_NONPOSS) + ".");
    return FALSE;
\}

if(\$actor."combat:weapon:mainhand") \{
    mainhand = \$actor."combat:weapon:mainhand";

    if(weapon == mainhand) \{
        EmitTo(\$actor, "You are already wielding " + Describe(weapon, nil, nil, STYLE_NONPOSS) + " in your main hand.");
        return FALSE;
    \}
\}

if(\$actor."combat:weapon:offhand") \{
    offhand = \$actor."combat:weapon:offhand";

    if(weapon == mainhand) \{
        EmitTo(\$actor, "You are already wielding " + Describe(weapon, nil, nil, STYLE_NONPOSS) + " in your off hand.");
        return FALSE;
    \}
\}

if(mainhand \&\& offhand) \{
    EmitTo(\$actor, "You cannot wield any more weapons.");
    return FALSE;
\}

size = Int(weapon."combat:weapon:size");

/* Two-handed weapon */
if(size \> 1) \{
    if(mainhand) \{
        EmitTo(\$actor, Describe(weapon, nil, nil, STYLE_NONPOSS) + " requires two hands to wield. You are already wielding " + Describe(mainhand) + " in your main hand.");
        return FALSE;
    \}

    if(offhand) \{
        EmitTo(\$actor, Describe(weapon, nil, nil, STYLE_NONPOSS) + " requires two hands to wield. You are already wielding " + Describe(offhand) + " in your off hand.");
        return FALSE;
    \}

    EmitTo(\$actor, TAG("You wield " + Describe(weapon, nil, nil, STYLE_NONPOSS) + " in both hands.", "social:first"));
    EmitIn(\$actor."base:environment", TAG(Describe(\$actor) + " wields " + Describe(weapon, nil, nil, STYLE_NONPOSS) + " in both hands.", "social:third"), \$actor);
    combat::wield(\$mainhand: weapon, \$offhand: weapon);
\}

/* One-handed weapon */
if(size \<= 1) \{
    if(!mainhand) \{
        EmitTo(\$actor, TAG("You wield " + Describe(weapon, nil, nil, STYLE_NONPOSS) + " in your main hand.", "social:first"));
        EmitIn(\$actor."base:environment", TAG(Describe(\$actor) + " wields " + Describe(weapon, nil, nil, STYLE_NONPOSS) + " in " + Str(\$actor."base:possessive") + " main hand.", "social:third"), \$actor);
        combat::wield(\$mainhand: weapon);
    \}

    if(mainhand \&\& !offhand) \{
        if(\$actor."combat:shield") \{
            EmitTo(\$actor, "You cannot wield a weapon in your offhand while using a shield.");
            return FALSE;
        \} else \{
            EmitTo(\$actor, TAG("You wield " + Describe(weapon, nil, nil, STYLE_NONPOSS) + " in your off hand.", "social:first"));
            EmitIn(\$actor."base:environment", TAG(Describe(\$actor) + " wields " + Describe(weapon, nil, nil, STYLE_NONPOSS) + " in " + Str(\$actor."base:possessive") + " off hand.", "social:third"), \$actor);
            combat::wield(\$offhand: weapon);
        \}
    \}
\}

return TRUE;
      </Core:Property>
      <Core:Property property="revisions">
         (\{ 1144052081, "-", "SYNC", 1221752477, "???", "R", 1566589996, "jominey", "E", 1566590021, "jominey", "E", 1566590097, "jominey", "E", 1566590164, "jominey", "E", 1566590167, "jominey", "E", 1566590217, "jominey", "E", 1566594198, "jominey", "E", 1566594233, "jominey", "E", 1566594261, "jominey", "E", 1566594345, "jominey", "E", 1566594406, "jominey", "E", 1566594447, "jominey", "E", 1566600196, "jominey", "E", 1566600218, "jominey", "E", 1566600306, "jominey", "E", 1566601031, "jominey", "E", 1566601660, "jominey", "E", 1566866078, "jominey", "E", 1566955361, "jominey", "E" \})
      </Core:Property>
    </Core:PCProperties>
  </SkotOS:MerAcHa>
</object>
