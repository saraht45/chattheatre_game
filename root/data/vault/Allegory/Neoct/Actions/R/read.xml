<object clone="/usr/SkotOS/obj/meracha" owner="TextIF">
  <SkotOS:MerAcHa action_id="read">
    <Core:PCProperties>
      <Core:Property property="merry:global-post:action">
         X[M] object scroll;
string det, writing, txt, signature;
SAM desc, title;

scroll = NRefOb(\$what[0]);
det = NRefDetail(\$what[0]);

/*
 * \<zwoc\> Make reading other's scrolls -visible-
 * to the holder of the scroll.
 */

if( scroll."base:holder" != \$actor ) \{
  EmitTo( scroll."base:holder", Describe(\$actor)+" reads "+Describe(scroll,nil,scroll."base:holder")+"." );
\}
/*
 * \</zwoc\>
 */

if (!\$actor \|\| !scroll) \{
  return TRUE;
\}

desc = Get(scroll, "details:" + det + ":descriptions:delivery-to");
if (desc) \{
  txt = UnSAM(desc);
  if (txt \&\& strlen(txt)) \{
    EmitTo(\$actor, "It is addressed to " + capitalize(txt) + ".");
  \}
\}

\$seal = scroll.brokenseal;
if( \$seal )\{
  EmitTo( \$actor, "The " + \$seal + " has been broken." );
\}

txt = nil;

desc = Get(scroll, "details:" + det + ":descriptions:writing");
if(scroll."trait:objecttype" != "book" \&\& scroll."trait:objecttype" != "newspaper" \&\& desc) \{
  writing = UnSAM(desc);
  if (writing \&\& strlen(writing)) \{
    if (writing[0] == '\\"') \{
      writing = writing[1 ..];
    \}
    if (writing[strlen(writing)-1] == '\\"') \{
      writing = writing[.. strlen(writing)-2];
    \}

    if(\$langstr = ::get_lang(\$text: writing)) \{
        EmitTo(\$actor, " It says in " + \$langstr + ":\\n\\n" + writing + "\\n");
    \} else \{
        EmitTo(\$actor, " It says:\\n\\n" + writing + "\\n");
    \}
  \}
\} else \{
    desc = Get(scroll, "trait:" + det);
    title = Get(scroll, "trait:" + det + ":title");
 
    if(desc) \{
        writing = UnSAM(desc);
        if (writing \&\& strlen(writing)) \{
            if (writing[0] == '\\"') \{
                writing = writing[1 ..];
            \}
            if (writing[strlen(writing)-1] == '\\"') \{
                writing = writing[.. strlen(writing)-2];
            \}
 
            if(title) \{
                \$titlestr = " titled \\"" + UnSAM(title) + "\\""; 
            \}

            if(\$langstr = ::get_lang(\$text: writing)) \{
                EmitTo(\$actor, " Upon the page" + Str(\$titlestr) + " is written in " + \$langstr + ":\\n\\n" + replace_strings(writing, "\&lt;br/\&gt;", "\\n") + "\\n");
            \} else \{
                EmitTo(\$actor, " Upon the page" + Str(\$titlestr) + " is written:\\n\\n" + replace_strings(writing, "\&lt;br/\&gt;", "\\n") + "\\n");
            \}
       \}
   \}
\}

if (!writing) \{
  if(scroll."trait:objecttype" == "scroll")
      EmitTo(\$actor, " There is nothing written on it.\\n");
  else
      EmitTo(\$actor, " Nothing is written upon the page.\\n");
\}

desc = Get(scroll, "details:" + det + ":descriptions:delivery-from");
if (desc) \{
  txt = UnSAM(desc);
  if (txt \&\& strlen(txt)) \{
    desc = Get(scroll, "details:" + det + ":descriptions:delivery-end");
    if (desc) \{
      signature = UnSAM(desc);
      if (signature \&\& strlen(signature)) \{
        EmitTo(\$actor, "It is signed, \\"" + signature + "\\", by " +
               capitalize(txt) + ".\\n");
        return TRUE;
      \}
    \}
    EmitTo(\$actor, "It is signed by " + capitalize(txt) + ".\\n");
    return TRUE;
  \}
\}
if (writing \&\& scroll.writable) \{
  if(scroll."trait:objecttype" == "scroll") EmitTo(\$actor, "It is unsigned.\\n");
\}
return TRUE;
      </Core:Property>
      <Core:Property property="merry:global-pre:action">
         X[M] object scroll;
string det, writing, txt, signature;
SAM desc;

scroll = NRefOb(\$what[0]);
det = NRefDetail(\$what[0]);

if( NRefOb( \$target ).sealed ) \{
    EmitTo( \$actor, Describe( \$target, nil, \$actor ) + " is not open. You need to break the seal or open " + Describe(\$target, nil, \$actor) + " to read it." );
    desc = Get(scroll, "details:" + det + ":descriptions:delivery-to");

    if (desc) \{
        txt = UnSAM(desc);
        if (txt \&\& strlen(txt)) \{
            EmitTo(\$actor, "It is addressed to " + capitalize(txt) + ".");
        \}
    \}
    return FALSE;
\} else \{
    return TRUE;
\}
      </Core:Property>
      <Core:Property property="merry:global:action">
         X[M] object scroll;  
string det;  
SAM desc;  

scroll = NRefOb(\$what[0]);  
det = NRefDetail(\$what[0]);  
  
/* check for arturl */  
desc = Get(scroll, "details:" + det + ":descriptions:arturl");  
if (desc) \{ 
  Act(\$actor, "client_control", \$id: 2, \$val: UnSAM(desc)); 
\}  
  
return TRUE;
      </Core:Property>
      <Core:Property property="merry:lib:get_lang">
         X[M] string lang;
if(sscanf(\$text, "\\000%s\|%d\\001%s\\002", lang, \$one, \$two) == 3) \{
    sscanf(\$text, "\\000%s\|%d\\001%s\\002", lang, \$one, \$two);
    return capitalize(lang[1..]);
\}

return nil;
      </Core:Property>
      <Core:Property property="revisions">
         (\{ 1056560985, "zell", "E", 1056561030, "zell", "E", 1089006326, "kargh", "P", 1089006729, "kargh", "P", 1089006995, "kargh", "P", 1089007088, "kargh", "P", 1192241859, "tomdowd", "P", 1192242019, "tomdowd", "P", 1221752476, "???", "R", 1569977296, "jominey", "E", 1569977371, "jominey", "E", 1569977414, "jominey", "E", 1569977493, "jominey", "E", 1569977553, "jominey", "E", 1569977610, "jominey", "E", 1570070943, "jominey", "R", 1570071738, "jominey", "E", 1570072513, "jominey", "E", 1570072627, "jominey", "E", 1570072701, "jominey", "E", 1570285307, "jominey", "E", 1570285586, "jominey", "E", 1570285721, "jominey", "E", 1570285747, "jominey", "E", 1570285789, "jominey", "E", 1570285989, "jominey", "E", 1570286041, "jominey", "E", 1570323105, "jominey", "E", 1572061563, "jominey", "E", 1573325859, "jominey", "E", 1573325890, "jominey", "E", 1573325908, "jominey", "E", 1573325927, "jominey", "E", 1573326031, "jominey", "E", 1573326075, "jominey", "E", 1573326103, "jominey", "E", 1573326136, "jominey", "E", 1573326993, "jominey", "E", 1573327027, "jominey", "E", 1573327215, "jominey", "E", 1573327246, "jominey", "E", 1573327281, "jominey", "E", 1573327379, "jominey", "E", 1573327454, "jominey", "E", 1573328855, "jominey", "E", 1573332537, "jominey", "E", 1573343110, "jominey", "P", 1573506773, "jominey", "E", 1573863223, "sendnoodlez", "E", 1585778177, "jominey", "E", 1585778724, "jominey", "E" \})
      </Core:Property>
    </Core:PCProperties>
  </SkotOS:MerAcHa>
</object>
